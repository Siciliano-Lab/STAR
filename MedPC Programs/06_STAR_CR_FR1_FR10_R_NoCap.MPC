\Outputs

^House_Light = 7
^Dispenser = 14
^LNP_Light = 1
^RNP_Light = 2
\^Dip_Light =
^Left_Light = 4
^Right_Light = 5
^Tone1 = 9
^Tone2 = 10
^Tone3 = 11
^Tone4 = 12
^White_Noise = 8
^Shock = 13


\Inputs

^LNP = 1
^RNP = 2
^Lick = 3

\Variable

\A = Active Nosepoke (Left)
\B = Inactive Nosepoke (Right)
\C = Active Nosepoke ITI (Left)
\D = Inactive Nosepoke ITI (Right)
\E = Port Entry
\F = Port Exit
\G = Nosepoke Tracker
\H = Sucrose Deliveries
\I
SEALED_ARRAY J = 2000     \Active Nosepoke (Left) Timestamp
SEALED_ARRAY K = 2000     \Inactive Nosepoke (Right) Timestamp
SEALED_ARRAY L = 2000     \Active Nosepoke ITI (Left) Timestamp
SEALED_ARRAY M = 2000     \Inactive Nosepoke ITI (Right) Timestamp
SEALED_ARRAY N = 2000     \Port Entry Timestamp
SEALED_ARRAY O = 2000     \Port Exit Timestamp
\P
\Q
\R
\S
\T = Session Timer
\U
\V
\W
\X
\Y = Lickometer Tracker
\Z


S.S.1,

S1,

     0.01": OFF ^Dispenser ---> S2

S2,

    #START: ON ^White_Noise, ^LNP_Light, ^RNP_Light; OFF ^Dispenser ---> S3

S3,

    #R^RNP: SET J(A) = T; ADD A, H; ON ^Dispenser; OFF ^LNP_Light, ^RNP_Light ---> S4
    #R^LNP: SET K(B) = T; ADD B ---> SX

S4,

   10": OFF ^Dispenser; ON ^LNP_Light, ^RNP_Light ---> S5
      #R^RNP: SET L(C) = T; ADD C ---> SX
      #R^LNP: SET M(D) = T; ADD D ---> SX

S5,

    #R^RNP: SET J(A) = T; ADD A, G;
            IF G = 10 [@SUC, @NOSUC]
                  @SUC: ON ^Dispenser; OFF ^LNP_Light, ^RNP_Light; ADD H; SET G = 0 ---> S6
                  @NOSUC: ---> SX
    #R^LNP: SET K(B) = T; ADD B ---> SX

S6,

   10": OFF ^Dispenser; ON ^LNP_Light, ^RNP_Light ---> S5
      #R^RNP: SET L(C) = T; ADD C ---> SX
      #R^LNP: SET M(D) = T; ADD D ---> SX


S.S.2,

S1,
    #START: ---> S2

S2,

    #R^Lick: ADD Y; IF Y >= 2 [@Lick, @NoLick]
                 @Lick: Z3; SET Y = 0 ---> S3
                 @NoLick: ---> SX
    0.02": SET Y = 0 ---> S2

S3,

    0.02": Z4 ---> S2
    #R^Lick: ---> S3

S.S.3,

S1,
    #Z3: SET N(E) = T; ADD E; SHOW 5, Port Entry, E ---> S1
\After port exit

S.S.4,

S1,
    #Z4: SET O(F) = T; ADD F; SHOW 6, Port Exit, F ---> SX

S.S.5,

S1,

    #START ---> S2

S2,

    0.01": SET T = T + 0.01; SHOW 1, Active, A, 2, Inactive, B, 3, Active ITI, C, 4, Inactive ITI, D, 7, Timer, T, 8, Sucrose, H ---> SX

S.S.6,

S1,

    #START ---> S2

S2,

    60' ---> STOPSAVE




